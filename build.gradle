buildscript {
	ext {
		springBootVersion = '2.7.3'
		set('springCloudVersion', "2021.0.4")
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
		classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
	}
}

// 하위 모든 프로젝트 공통 세팅
subprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	group = 'com.assignment'
	version = '0.0.1-SNAPSHOT'

	sourceCompatibility = '11'
	targetCompatibility = '11'
	compileJava.options.encoding = 'UTF-8'

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	// 하위 모듈에서 공통으로 사용하는 세팅 추가
	dependencies {
		//Request, Response 컨트롤을 위해 사용
		implementation('org.springframework.boot:spring-boot-starter-web') {
			exclude(module: 'jackson-databind')
		}
		implementation 'com.fasterxml.jackson.core:jackson-databind'
		implementation 'com.fasterxml.jackson.core:jackson-annotations'
		implementation 'com.fasterxml.jackson.core:jackson-core'

		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

		//p6spy
		implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.7.1'

		//jasypt
		implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.4'

		//OpenFeign
		implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'

		//ehcache
		implementation 'org.springframework.boot:spring-boot-starter-cache'
		implementation group: 'net.sf.ehcache', name: 'ehcache', version: '2.9.1'

		compileOnly 'org.projectlombok:lombok'
		runtimeOnly 'com.h2database:h2'
		annotationProcessor 'org.projectlombok:lombok'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:2021.0.4"
		}
	}

	tasks.named('test') {
		useJUnitPlatform()
	}
}

// blog-core
project(':blog-core') {
	bootJar { enabled = false } // core 은 bootJar로 패키징 안함
	jar { enabled = true }

	dependencies {
	}
}

// blog-api
project(':blog-api') {
	bootJar { enabled = true }
	jar { enabled = false }

	dependencies {
		implementation project(':blog-core') // 컴파일 시 blog-core project 로딩
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-validation'
	}
}